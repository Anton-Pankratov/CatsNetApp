plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jlleitschuh.gradle.ktlint' version "10.2.0"
    id 'io.gitlab.arturbosch.detekt' version "1.18.1"
}

android {
    compileSdk 30

    defaultConfig {
        applicationId "net.app.catsnetapp"
        minSdk 21
        targetSdk 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "BASE_URL", base_url
        buildConfigField "String", "API_KEY", api_key
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

ext {

    /** Android API Extension */
    coreKtx = '1.6.0'

    /** APIs Support */
    appCompat = '1.3.1'

    /** Design */
    materialDesign = '1.4.0'
    glide = '4.12.0'

    /** Coroutines */
    coroutines = '1.4.2'

    /** Lifecycle */
    lifecycle = '2.3.1'
    lifecycleExt = '2.2.0'

    /** Dependency Injection*/
    koin = '3.1.2'

    /** Networking */
    retrofit = '2.9.0'

    /** OkHttp Client */
    okHttp = '4.9.1'

    /** Parser */
    moshi = '1.12.0'

    /** Linters */
    ktlint = '0.42.0'
    detekt = '1.18.1'

    /** Testing */
    junit = '4.13.2'
    junitInstrumental = '1.1.3'
    espresso = '3.4.0'
}

ktlint {
    version = "$ktlint"
    debug = true
    verbose = true
    android = false
    outputToConsole = true
    outputColorName = "RED"
    ignoreFailures = true
    enableExperimentalRules = true
    additionalEditorconfigFile = file("/some/additional/.editorconfig")
    disabledRules = ["final-newline"]
    baseline = file("my-project-ktlint-baseline.xml")
    reporters {
        reporter "plain"
        reporter "checkstyle"
        reporter "sarif"
    }
    kotlinScriptAdditionalPaths {
        include fileTree("scripts/")
    }
    filter {
        exclude("**/generated/**")
        include("**/kotlin/**")
    }
}

detekt {
    toolVersion = "$detekt"
    config = files("config/detekt/detekt.yml")
    buildUponDefaultConfig = true
    ignoreFailures = true

    reports {
        xml {
            enabled = true
            destination = file("path/to/destination.xml")
        }
        html {
            enabled = true
            destination = file("path/to/destination.html")
        }
        txt {
            enabled = true
            destination = file("path/to/destination.txt")
        }
    }
}

tasks.register('printLintersProperties') {
    doLast {
        println ktlint
        println detekt
    }
}

tasks.register('drawablesHashcodes') {
    doLast {
        file('src/main/res/drawable').listFiles().each { File file ->
            println "$file.name -> hashCode is ${file.hashCode()}"
        }
    }
}

dependencies {

    /** Android API Extension */
    implementation "androidx.core:core-ktx:$coreKtx"

    /** APIs Support */
    implementation "androidx.appcompat:appcompat:$appCompat"

    /** Lifecycle */
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleExt"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle"
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:$lifecycle"

    /** Coroutines */
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines"

    /** Design */
    implementation "com.google.android.material:material:$materialDesign"
    implementation "com.github.bumptech.glide:glide:$glide"

    /** Dependency Injection */
    implementation "io.insert-koin:koin-android:$koin"

    /** Networking */
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit"

    /** OkHttp Client */
    implementation "com.squareup.okhttp3:okhttp:$okHttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttp"

    /** Parser */
    implementation "com.squareup.moshi:moshi-kotlin:$moshi"
    implementation "com.squareup.moshi:moshi-adapters:$moshi"

    implementation "com.treebo:internetavailabilitychecker:1.0.4"

    /** Testing */
    testImplementation "junit:junit:$junit"
    androidTestImplementation "androidx.test.ext:junit:$junitInstrumental"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso"
}